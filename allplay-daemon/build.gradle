apply plugin: "application"
apply plugin: "kotlin"

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "com.github.ajalt:clikt:2.7.1"
    implementation "io.ktor:ktor-gson:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
}

application {
    mainClassName "com.dicedmelon.allplay.daemon.Main"
}

startScripts {
    defaultJvmOpts = ["-Djava.library.path=MY_APP_HOMElib/ARCH"]
    doLast {
        unixScript.text = unixScript.text
                .replace('MY_APP_HOME', '\$APP_HOME/')
                .replace('ARCH', '\$(uname -m)')
        windowsScript.text = windowsScript.text
                .replace('MY_APP_HOME', '%APP_HOME%\\')
                .replace('ARCH', '%APP_HOME%\\')
    }
}

run {
    String arch = System.getProperty("os.arch")
    systemProperty "java.library.path", file("src/dist/lib/$arch")
}
